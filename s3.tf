resource "aws_s3_bucket" "main" {
  provider = "aws.main"
  bucket   = "${var.fqdn}"
  acl      = "private"
  policy   = "${data.aws_iam_policy_document.bucket_policy.json}"

  website {
    index_document = "${var.index_document}"
    error_document = "${var.error_document}"
    routing_rules  = "${var.routing_rules}"
  }

  force_destroy = "${var.force_destroy}"

  tags = "${merge("${var.tags}",map("Name", "${var.fqdn}"))}"
}

data "aws_iam_policy_document" "bucket_policy" {
  provider = "aws.main"

  statement {
    sid = "AllowedIPReadAccess"

    actions = [
      "s3:GetObject",
    ]

    resources = [
      "arn:aws:s3:::${var.fqdn}/*",
    ]

    condition {
      test     = "IpAddress"
      variable = "aws:SourceIp"

      values = [
        "${var.allowed_ips}",
      ]
    }

    principals {
      type        = "*"
      identifiers = ["*"]
    }
  }

  statement {
    sid = "AllowCFOriginAccess"

    actions = [
      "s3:GetObject",
    ]

    resources = [
      "arn:aws:s3:::${var.fqdn}/*",
    ]

    condition {
      test     = "StringEquals"
      variable = "aws:UserAgent"

      values = [
        "${var.refer_secret}",
      ]
    }

    principals {
      type        = "*"
      identifiers = ["*"]
    }
  }
}
